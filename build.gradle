plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
}

group 'com.github.mdashl'
version '1.2.0'

repositories {
    jcenter()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.squareup.okhttp3:okhttp:3.13.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8'
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            artifactId = 'hypixel-api'

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'hypixel-api'
                description = 'Kotlin Hypixel API wrapper'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://choosealicense.com/licenses/mit/'
                    }
                }
                developers {
                    developer {
                        id = 'mdashlw'
                        name = 'Mdashlw'
                        email = 'mdashlw@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/mdashl/hypixel-api.git'
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['BintrayRelease']

    publish = true
    override = true

    pkg {
        repo = 'maven'
        name = 'hypixel-api'
        desc = 'Kotlin Hypixel API wrapper'
        issueTrackerUrl = 'https://github.com/mdashl/hypixel-api/issues'
        vcsUrl = 'https://github.com/mdashl/hypixel-api.git'
        githubRepo = 'https://github.com/mdashl/hypixel-api.git'
        githubReleaseNotesFile = 'README.md'
        publicDownloadNumbers = false
        licenses = ['MIT']
        labels = ['Hypixel', 'Kotlin']
    }
}
